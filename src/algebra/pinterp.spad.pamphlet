\documentclass{article}
\usepackage{axiom}
\begin{document}
\title{\$SPAD/src/algebra pinterp.spad}
\author{The Axiom Team}
\maketitle
\begin{abstract}
\end{abstract}
\eject
\tableofcontents
\eject
\section{package PINTERPA PolynomialInterpolationAlgorithms}
<<package PINTERPA PolynomialInterpolationAlgorithms>>=
)abbrev package PINTERPA PolynomialInterpolationAlgorithms
++ Description:
++ This package exports interpolation algorithms
PolynomialInterpolationAlgorithms(F, P): Cat == Body   where
    F: Field
    P: UnivariatePolynomialCategory(F)
 
    Cat ==> with
        LagrangeInterpolation: (List F, List F) -> P
		++ LagrangeInterpolation(l1,l2) \undocumented
 
    Body ==> add
        LagrangeInterpolation(lx, ly) ==
            #lx ^= #ly =>
                error "Different number of points and values."
            ip: P := 0
            for xi in lx for yi in ly for i in 0.. repeat
                pp: P := 1
                xp: F := 1
                for xj in lx for j in 0.. | i ^= j repeat
                    pp := pp * (monomial(1,1) - monomial(xj,0))
                    xp := xp * (xi - xj)
                ip := ip + (yi/xp) * pp
            ip

@
\section{package PINTERP PolynomialInterpolation}
<<package PINTERP PolynomialInterpolation>>=
)abbrev package PINTERP PolynomialInterpolation
++ Description:
++ This package exports interpolation algorithms
PolynomialInterpolation(xx, F): Cat == Body   where
    xx: Symbol
    F:  Field
    UP  ==> UnivariatePolynomial
    SUP ==> SparseUnivariatePolynomial
 
    Cat ==> with
        interpolate: (UP(xx,F), List F, List F) -> UP(xx,F)
		++ interpolate(u,lf,lg) \undocumented
        interpolate: (List F, List F)           -> SUP F
		++ interpolate(lf,lg) \undocumented
 
    Body ==> add
        PIA ==> PolynomialInterpolationAlgorithms
 
        interpolate(qx, lx, ly) ==
            px := LagrangeInterpolation(lx, ly)$PIA(F, UP(xx, F))
            elt(px, qx)
 
        interpolate(lx, ly) ==
            LagrangeInterpolation(lx, ly)$PIA(F, SUP F)

@
\section{License}
<<license>>=
--Copyright (c) 1991-2002, The Numerical ALgorithms Group Ltd.
--All rights reserved.
--
--Redistribution and use in source and binary forms, with or without
--modification, are permitted provided that the following conditions are
--met:
--
--    - Redistributions of source code must retain the above copyright
--      notice, this list of conditions and the following disclaimer.
--
--    - Redistributions in binary form must reproduce the above copyright
--      notice, this list of conditions and the following disclaimer in
--      the documentation and/or other materials provided with the
--      distribution.
--
--    - Neither the name of The Numerical ALgorithms Group Ltd. nor the
--      names of its contributors may be used to endorse or promote products
--      derived from this software without specific prior written permission.
--
--THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
--IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
--TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
--PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER
--OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
--EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
--PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
--PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
--LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
--NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
--SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
@
<<*>>=
<<license>>
 
<<package PINTERPA PolynomialInterpolationAlgorithms>>
<<package PINTERP PolynomialInterpolation>>
@
\eject
\begin{thebibliography}{99}
\bibitem{1} nothing
\end{thebibliography}
\end{document}
